Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Compare Database
Option Explicit

Implements IAppender


Private m_name As String
Private m_threshold As Level

Private Sub Class_Initialize()
    Set m_threshold = Levels.Dbug
    m_name = "MsgBoxAppender"
End Sub

Public Property Get IAppender_Name() As String
    IAppender_Name = m_name
End Property
Public Property Let IAppender_Name(ByVal sName As String)
    m_name = sName
End Property

Public Sub IAppender_DoAppend(ByVal oLoggingEvent As LoggingEvent)
    With oLoggingEvent
        Dim s As String
        If .Level.value >= m_threshold.value Then
            s = "[" & .LoggerName & "] " & .Level.Name & vbTab & .TimeStamp & vbCrLf & oLoggingEvent.Message
            If .ErrorNumber <> 0 Then
                s = s & vbCrLf & .ErrorNumber & vbCrLf & .ErrorDescription & vbCrLf & .ErrorSource
                If .ErrorLine > 0 Then
                    s = s & vbTab & "Line " & .ErrorLine
                End If
            End If
            MsgBox s
        End If
    End With
End Sub
Public Property Get Threshold() As Level
    Set Threshold = m_threshold
End Property
Public Property Set Threshold(ByVal oThreshold As Level)
    Set m_threshold = oThreshold
End Property